apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

def PACKAGE_NAME = 'com.example.kotlin'
android {
    sourceSets.all { java.srcDirs += "src/${name}/kotlin" }
    packagingOptions {
        exclude 'META-INF/rxjava.properties'
    }
    compileSdkVersion 25
    buildToolsVersion "25.0.3"
    dataBinding {
        enabled = true
    }
    defaultConfig {
        applicationId "${PACKAGE_NAME}"
        minSdkVersion 15
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    productFlavors {
        production {
            applicationId "${PACKAGE_NAME}"
        }
        local {
            applicationId "${PACKAGE_NAME}.local"
        }
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    def androidSupport = ['appcompat-v7', 'design']
    androidSupport.each { name ->
        compile "com.android.support:$name:25.3.1"
    }
    compile 'com.android.support.constraint:constraint-layout:1.0.2'

    testCompile 'junit:junit:4.12'
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
    kapt 'com.android.databinding:compiler:2.3.0'

    def kodein = ['kodein', 'kodein-android']
    kodein.each { name ->
        compile "com.github.salomonbrys.kodein:$name:4.0.0"
    }

    def kbinding = ['kbinding', 'kbinding-common', 'kbinding-adapterview', 'kbinding-support-v4']
    kbinding.each { name ->
        compile "com.benny.library:$name:0.2.3"
    }
    kapt 'com.benny.library:kbinding-compiler:0.2.3'

    def anko = ['sdk15', 'support-v4', 'design', 'appcompat-v7', 'recyclerview-v7', 'percent', 'cardview-v7', 'commons']
    anko.each { name ->
        compile "org.jetbrains.anko:anko-$name:0.10.1"
    }

    def fuel = ['fuel', 'fuel-android', 'fuel-rxjava', 'fuel-livedata', 'fuel-gson']
    fuel.each { name ->
        compile "com.github.kittinunf.fuel:$name:1.9.0"
    }

    def rxlifecycle = ['rxlifecycle', 'rxlifecycle-android', 'rxlifecycle-components']
    rxlifecycle.each { name ->
        compile("com.trello:$name:1.0") {
            exclude group: 'com.google.code.findbugs'
        }
    }

    compile 'com.sackcentury:shinebutton:0.1.9'
    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'
}

kapt {
    generateStubs = true
}

repositories {
    mavenCentral()
    maven { url 'https://maven.google.com' }
}
